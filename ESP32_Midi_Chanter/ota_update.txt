#include "ota_update.h"
#include "config.h"

#include <WiFi.h>
#include <WiFiClient.h>
#include <WebServer.h>
#include <ESPmDNS.h>
#include <Update.h>
#include <ArduinoOTA.h>

WebServer server(80);

// Simple Web OTA page
static const char* loginIndex =
"<h3>Chanter OTA Update</h3>"
"<form method='POST' action='/update' enctype='multipart/form-data'>"
"<input type='file' name='update'>"
"<input type='submit' value='Upload New Sketch'>"
"</form>";

void otaSetup() {
  Serial.println("Starting OTA system...");

  WiFi.mode(WIFI_STA);
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);

  while (WiFi.waitForConnectResult() != WL_CONNECTED) {
    Serial.println("WiFi Failed! Rebooting...");
    delay(5000);
    ESP.restart();
  }

  if (!MDNS.begin(HOST_NAME)) {
    Serial.println("Error setting up MDNS responder!");
  }

  ArduinoOTA.setHostname(HOST_NAME);

  ArduinoOTA.onStart([]() {
    Serial.println("OTA Starting...");
  });

  ArduinoOTA.onEnd([]() {
    Serial.println("\nOTA Finished!");
  });

  ArduinoOTA.begin();

  server.on("/", HTTP_GET, []() {
    server.sendHeader("Connection", "close");
    server.send(200, "text/html", loginIndex);
  });

  server.on("/update", HTTP_POST,
    []() {
      server.sendHeader("Connection", "close");
      server.send(200, "text/plain",
                  (Update.hasError()) ? "Update Failed" : "Update Success! Rebooting...");
      delay(1000);
      ESP.restart();
    },
    []() {
      HTTPUpload& upload = server.upload();
      if (upload.status == UPLOAD_FILE_START) {
        if (!Update.begin(UPDATE_SIZE_UNKNOWN))
          Update.printError(Serial);
      } else if (upload.status == UPLOAD_FILE_WRITE) {
        if (Update.write(upload.buf, upload.currentSize) != upload.currentSize)
          Update.printError(Serial);
      } else if (upload.status == UPLOAD_FILE_END) {
        if (Update.end(true))
          Serial.printf("Update Success: %u bytes\n", upload.totalSize);
        else
          Update.printError(Serial);
      }
    }
  );

  server.begin();

  Serial.print("OTA Ready. IP address: ");
  Serial.println(WiFi.localIP());
}

void otaLoop() {
  ArduinoOTA.handle();
  server.handleClient();
}
